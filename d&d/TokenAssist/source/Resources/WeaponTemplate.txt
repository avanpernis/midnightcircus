<!-- generic variables that were populated by TokenAssist -->
[H : PowerName = "__POWER_NAME__"]
[H : AttackBonus = "__ATTACK_BONUS__"]
[H : Damage = "__DAMAGE__"]
[H : MaxDamage = eval("__MAX_DAMAGE__")]
[H : MultipleTargets = __MULTIPLE_TARGETS__]

<!-- html power card, as found in the compendium -->
[H : PowerCard = "__POWER_CARD__"]

<!-- retrieve previously stored values -->
[H : NumberOfTargets = getStrProp(CombatStatus, "LastNumberOfTargets")]
[H : CombatAdvantage = getStrProp(CombatStatus, "LastCombatAdvantage")]
[H : TargetCover = getStrProp(CombatStatus, "LastTargetCover")]
[H : TargetConcealment = getStrProp(CombatStatus, "LastTargetConcealment")]
[H : TempAttackBonus = getStrProp(CombatStatus, "LastTempAttackBonus")]
[H : TempDamageBonus = getStrProp(CombatStatus, "LastTempDamageBonus")]

<!-- prompt for target information, tactical state, temporary bonuses, etc... -->
[H : InputPrompt = input(
   "Attack Bonus: " + AttackBonus + ", Damage: " + Damage + " | <html>" + PowerCard + "</html> | | LABEL | SPAN = TRUE",
   "UNUSED | <html></html> | | LABEL | SPAN = TRUE",
   if(MultipleTargets == 1, "NumberOfTargets | " + NumberOfTargets + " | Number Of Targets | TEXT", ""),
   "CombatAdvantage | " + CombatAdvantage + " | Combat Advantage | CHECK",
   "TargetCover | None, Partial, Superior | Target Cover | RADIO | SELECT = " + TargetCover,
   "TargetConcealment | None, Partial, Total | Target Concealment | RADIO | SELECT = " + TargetConcealment,
   "TempAttackBonus | " + TempAttackBonus + " | Temporary Attack Bonus | TEXT",
   "TempDamageBonus | " + TempDamageBonus + " | Temporary Damage Bonus | TEXT")]

<!-- bail if the user cancels the dialog -->
[H : abort(InputPrompt)]

<!-- store the values for next time -->
[H : PropsUsedList = "NumberOfTargets, CombatAdvantage, TargetCover, TargetConcealment, TempAttackBonus, TempDamageBonus"]
[H, C(listCount(PropsUsedList)) : CombatStatus = setStrProp(CombatStatus, "Last" + listGet(PropsUsedList, roll.count), eval(listGet(PropsUsedList, roll.count)))]

<!-- show the power card to the other players -->
{PowerCard}

<!-- apply combat advantage, cover, concealment, and temporary attack bonus
[H: AttackBonus = AttackBonus
   + if(CombatAdvantage, 2, 0)
   - if(TargetCover == 0, 0, if(TargetCover == 1, 2, 5))
   - if(TargetConcealment == 0, 0, if(TargetConcealment == 1, 2, 5))
   + TempAttackBonus]

<!-- apply temporary damage bonus -->
[H: Damage = Damage + "+" + TempDamageBonus]

<!-- attack roll -->
[H: AttackRoll = 1d20]
[H: AttackResult = AttackRoll + AttackBonus]

<!-- damage roll -->
[H: CriticalRange = 20]
[H: DamageRoll = if(AttackRoll >= CriticalRange, MaxDamage, eval(Damage))]

<b>Attack: </b>{AttackResult}<br>
<b>Damage: </b>{DamageRoll}